def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

project.ext {
    supportLibrary = "27.0.0"
    multidex = "1.0.1"
    archComponent = "1.1.0"
    pagingComponent = "1.0.0-alpha5"
    dagger = "2.14.1"
    butterKnife = "8.8.1"
    rxJava = "2.1.6"
    rxBinding = "2.0.0"
    rxAndroid = "2.0.1"
    rxBroadCast = "2.0.0"
    timber = "4.6.0"
    constraint = "1.0.2"
    google = "11.6.0"
    oneSignal = "3.6.5"
    retrofit = "2.3.0"
    moshi = "1.5.0"
    okHttp = "3.9.1"
    autoValue = "1.5"
    autoMoshi = "0.4.5"
    autoRedacted = "1.0.1"
    autoWith = "1.0.0"
    autoParceler = "0.2.6"
    fresco = "1.5.0"
    frescoProcessor = "2.1.0"
    leakCanary = "1.5.4"
    btmNav = "2.1.0"
    store = "3.0.0"
}

def testLibs = dependencyGroup {

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

def support = dependencyGroup {
    implementation "com.android.support:appcompat-v7:$project.supportLibrary"
    implementation "com.android.support:design:$project.supportLibrary"
    implementation "com.android.support:support-v13:$project.supportLibrary"
    implementation "com.android.support:customtabs:$project.supportLibrary"
    implementation "com.android.support.constraint:constraint-layout:$project.constraint"

    debugImplementation "com.android.support:multidex:$project.multidex"
}

def archComponent = dependencyGroup {
    implementation "android.arch.lifecycle:extensions:$project.archComponent"
    implementation "android.arch.lifecycle:common-java8:$project.archComponent"
    implementation "android.arch.paging:runtime:$project.pagingComponent"
    implementation "android.arch.lifecycle:reactivestreams:$project.archComponent"

}

def analytics = dependencyGroup {

    implementation "com.google.firebase:firebase-core:$project.google"
    implementation "com.google.firebase:firebase-crash:$project.google"

//    implementation "com.onesignal:OneSignal:$project.oneSignal"
}

def google = dependencyGroup {
    implementation "com.google.android.gms:play-services-base:$project.google"
    implementation "com.google.android.gms:play-services-gcm:$project.google"
    implementation "com.google.android.gms:play-services-location:$project.google"
}

def RX = dependencyGroup {
    implementation "io.reactivex.rxjava2:rxandroid:$project.rxAndroid"
    implementation "io.reactivex.rxjava2:rxjava:$project.rxJava"

    implementation "com.cantrowitz:rxbroadcast:$project.rxBroadCast"
}

def rxBinding = dependencyGroup {
    implementation "com.jakewharton.rxbinding2:rxbinding:$project.rxBinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:$project.rxBinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$project.rxBinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$project.rxBinding"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$project.rxBinding"
}

def butterKnife = dependencyGroup {
    implementation "com.jakewharton:butterknife:$project.butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$project.butterKnife"
}

def autoValue = dependencyGroup {
    provided "com.jakewharton.auto.value:auto-value-annotations:$project.autoValue"

    annotationProcessor "com.google.auto.value:auto-value:$project.autoValue"
    annotationProcessor "com.gabrielittner.auto.value:auto-value-with:$project.autoWith"
    annotationProcessor "com.squareup.auto.value:auto-value-redacted:$project.autoRedacted"
}

def parceler = dependencyGroup {
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:$project.autoParceler"
    implementation "com.ryanharter.auto.value:auto-value-parcel-adapter:$project.autoParceler"
}

def moshi = dependencyGroup {
    implementation "com.squareup.moshi:moshi:$project.moshi"
    annotationProcessor "com.ryanharter.auto.value:auto-value-moshi:$project.autoMoshi"
    provided "com.ryanharter.auto.value:auto-value-moshi-annotations:$project.autoMoshi"
}

def dagger = dependencyGroup {
    implementation "com.google.dagger:dagger:$project.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$project.dagger"
}

def daggerAndroid = dependencyGroup {
    implementation "com.google.dagger:dagger-android-support:$project.dagger" // if you use the support libraries
    annotationProcessor "com.google.dagger:dagger-android-processor:$project.dagger"
}

def annotation = dependencyGroup {
    implementation 'org.jetbrains:annotations-java5:15.0'
    implementation 'javax.annotation:jsr250-api:1.0'
}

def retrofit = dependencyGroup {
    implementation "com.squareup.retrofit2:retrofit:$project.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$project.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$project.retrofit"
    implementation "com.squareup.okhttp3:okhttp:$project.okHttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$project.okHttp"
}

def timber = dependencyGroup {
    implementation "com.jakewharton.timber:timber:$project.timber"
}

def fresco = dependencyGroup {
    implementation "com.facebook.fresco:fresco:$project.fresco"
    implementation "jp.wasabeef:fresco-processors:$project.frescoProcessor"
}

def leakCanary = dependencyGroup {

    debugCompile "com.squareup.leakcanary:leakcanary-android:$project.leakCanary"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$project.leakCanary"
}

def store = dependencyGroup {
    implementation "com.nytimes.android:cache3:$project.store"
    implementation "com.nytimes.android:store3:$project.store"
    implementation "com.nytimes.android:middleware-moshi3:$project.store"
    implementation "com.nytimes.android:filesystem3:$project.store"
}

def ui = dependencyGroup {
    implementation  "com.aurelhubert:ahbottomnavigation:$project.btmNav"
//    implementation 'org.zakariya.stickyheaders:stickyheaders:0.7.6'
//    implementation 'com.bignerdranch.android:simple-item-decoration:1.0.0'
}

//noinspection GroovyUnusedAssignment
def job = dependencyGroup {
    implementation 'com.evernote:android-job:1.2.1'
}

//noinspection GroovyUnusedAssignment
def jwPlayer = dependencyGroup {
    implementation 'com.longtailvideo.jwplayer:jwplayer-core:+'
    implementation'com.longtailvideo.jwplayer:jwplayer-common:+'
}

//noinspection GroovyUnusedAssignment
def utils = dependencyGroup {
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC3'
    implementation 'com.beloo.widget:ChipsLayoutManager:0.3.7@aar'
    implementation 'net.alhazmy13.MediaPicker:libary:2.4.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testLibs()

    analytics()
    google()

    support()
    archComponent()

    RX()
    rxBinding()

    retrofit()

    store()

    autoValue()
    parceler()
    moshi()

    timber()

    butterKnife()
    dagger()
    daggerAndroid()

    fresco()

    leakCanary()

    annotation()

    ui()

    utils()
//    job()
//    jwPlayer()
}

